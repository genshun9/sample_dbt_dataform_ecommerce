config {
  type: "incremental",
  schema: "dataform_ecommerce_mart",
  tags: ["mart", "fact"],
  description: "注文ファクト",
  bigquery: {
    partitionBy: "order_date"
  }
}

-- Incremental処理のフィルタ条件を定義
pre_operations {
  DECLARE max_order_date TIMESTAMP;
  
  ${when(incremental(), `
    SET max_order_date = (SELECT MAX(order_created_at) FROM ${self()});
  `)}
}

WITH orders AS (
  SELECT
    order_id,
    user_id,
    status,
    created_at AS order_created_at
  FROM ${ref("stg_orders")}
  ${when(incremental(), `WHERE created_at > (SELECT MAX(order_created_at) FROM ${self()})`)}
),

order_items AS (
  SELECT
    order_id,
    inventory_item_id,
    sale_price,
    created_at
  FROM ${ref("stg_order_items")}
  ${when(incremental(), `WHERE created_at > (SELECT MAX(order_created_at) FROM ${self()})`)}
),

inventory AS (
  SELECT 
    inventory_item_id,
    cost AS product_cost
  FROM ${ref("stg_inventory_items")}
),

order_items_enhanced AS (
  SELECT
    order_items.order_id,
    order_items.sale_price,
    order_items.created_at,
    order_items.sale_price - COALESCE(inventory.product_cost, 0) AS gross_profit
  FROM order_items
  LEFT JOIN inventory USING (inventory_item_id)
),

order_items_agg AS (
  SELECT
    order_id,
    SUM(sale_price) AS total_sale_amount,
    SUM(gross_profit) AS total_gross_profit
  FROM order_items_enhanced
  GROUP BY order_id
)

SELECT
  orders.order_id,
  orders.user_id,
  orders.status,
  orders.order_created_at,
  DATE(orders.order_created_at) as order_date,
  COALESCE(oi.total_sale_amount, 0) AS total_sale_amount,
  COALESCE(oi.total_gross_profit, 0) AS total_gross_profit
FROM orders
LEFT JOIN order_items_agg AS oi USING (order_id)
